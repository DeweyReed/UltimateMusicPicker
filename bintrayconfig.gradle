if (!project.rootProject.file('local.properties').exists()) {
    println "Not applying bintrayconfig.gradle"
    return
}
apply plugin: 'com.novoda.bintray-release'

def getBintrayUserAndKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return [
            properties.getProperty("bintray.user"),
            properties.getProperty("bintray.apikey")
    ]
}

if (versions == null || versions.version_name == null) {
    throw new IllegalStateException("Unable to reference version_name")
}

task checkBintrayConfig {
    doLast {
        def (user, key) = getBintrayUserAndKey()
        if (user == null || user.isEmpty() || key == null || key.isEmpty()) {
            throw new IllegalStateException("Must specify Bintray user/API key in your local.properties.")
        }
    }
}

afterEvaluate {
    bintrayUpload.dependsOn checkBintrayConfig
}

def (user, key) = getBintrayUserAndKey()
publish {
    bintrayUser = user
    bintrayKey = key
    userOrg = 'reedreed'
    groupId = 'xyz.aprildown'
    artifactId = 'ultimatemusicpicker'
    publishVersion = versions.version_name
    licences = ['MIT License']
    desc = 'An Android music picker library for picking alarm, notification or ringtones sound using an Activity or a dialog.'
    website = 'https://github.com/DeweyReed/UltimateMusicPicker'
    issueTracker = "${website}/issues"
    repository = "${website}.git"
    dryRun = false
}